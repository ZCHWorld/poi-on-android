apply plugin: 'com.android.application'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:support-v4:27.1.0'
    implementation 'org.apache.commons:commons-text:1.3'
    // for unit-tests copied from Apache POI source
    implementation 'junit:junit:4.12'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.59'

    // newer versions of xmlsec are compiled with JDK 8 which does not work in Android Studio 2.x!
    implementation ('org.apache.santuario:xmlsec:2.0.0') {
        // break compilation because it includes stax-api which is under forbidden package 'javax.xml.stream'
        exclude group: 'org.codehaus.woodstox'
    }

    implementation files('../poishadow/build/libs/poishadow-all.jar')
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId 'org.dstadler.poiandroidtest.poitest'
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName '1.0'

        // Enabling multidex support.
        multiDexEnabled true
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // For the purposes of the sample, allow testing of a proguarded release build
            // using the debug key
            signingConfig signingConfigs.debug
        }
    }

    lintOptions {
        // There are restricted packages used in some of POI code, e.g. java.awt or javax.imageio
        // I could not exclude these in the
        abortOnError false
    }

    // error about duplicates if some of these are not excluded
    packagingOptions {
        pickFirst 'META-INF/DEPENDENCIES.txt'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/notice.txt'
        pickFirst 'META-INF/license.txt'
        pickFirst 'META-INF/dependencies.txt'
        pickFirst 'META-INF/LGPL2.1'
    }
}
